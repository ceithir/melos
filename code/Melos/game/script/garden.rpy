label stupor:
    "I know I must stop her before she really harms herself. However, once more, I'm hesitant on the \"how\"."

    "Yet, when she finishes her current dish and tries to grab the carafe one more time, I instinctively react. I catch the recipient myself and push it out of her reach. Her fingers close on the emptiness, and she bats an eyelid, disconcerted by this interruption of her freshly forged ritual."

    ambrosia "You've had enough I think."

    "My voice is soft, gentle. I force myself to look at her right into her eyes and to appear both kind and firm. I'm pretty sure this is too much for my acting skills, but I try nonetheless."

    "Antiope is still disturbed. She looks down at her food, but I put my open hand under her stare, like a barrier between her and the remaining snacks."

    ambrosia "That's. Enough."

    "Her face goes up again, and we look at each other. She is in a worst state that I thought. She seems absent, lost."

    "Then suddenly she rises from her seat and starts stumbling in an apparently random direction."

    "Seeing her in pain, I immediately stand up and help her. My left arm around her shoulders, I guide her to a calmer and cooler location. I can feel her weight pushed upon me for lack of ability to support herself alone, her erratic, hot, wine-laden breath against my neck, her moist skin under my palm. She is already quite sick."

    window hide
    show bg inside as trans_inside:
        linear 2.0 xpos -config.screen_width
    show bg garden as trans_garden:
        xpos config.screen_width
        linear 2.0 xpos 0
    pause 2.0
    scene bg garden
    window show

    "We laboriously walk together, trying not to bump into anybody, until we are finally met by the fresh air of the outside. A beautiful garden reveals itself before us, the plants, either medicinal or decorative, crowned with a surreal gray aura, the light of the moon in the darkness playing with the small layer of dew covering leaves and corollas."

    "The place was full of people earlier, but many hours have passed since, the night is now at its worst hours, and the area therefore deserted. There is a bench nearby, alongside a fountain, and I drag Antiope there. The stone is slightly humid, extremely cold, and I worry that letting her lie on it could be an incredibly stupid idea. But before I can move elsewhere, she just collapses there, trying to sit but ending lying on her back."

    "I rush to her side, but she stops me with a vindicative gesture of the hand, and straightens herself up through her own efforts. Her face is transfigured with rage, but against who or whom, only the gods know. She forces her own body to stand up again and walks a few steps away in the green."

    "There she throws up."

    show antiope:
        xalign 1.0 yalign 1.0
        linear 1.0 xalign 0.75
    pause 1.0
    show antiope:
        xalign 0.75

    "After that, she goes for the fountain, a little more steady on her feet, and washes her face in the icy water. Finally, she rests on the bench, properly sit this time. She looks {i}better{/i}, but she certainly does not look {i}fine{/i}. She at least has the decency of appearing embarrassed when she finally talks to me."

    antiope "I may have overdone it."

    "Anger fills me in. The most painful, burning, self-destructive variety. The kind only felt when someone dear to you endanger themselves without realizing how much their loss would affect those who love them.{w}\n\nI love Antiope with all my heart. And that's why at this very moment, I'm more furious than the three Erinyes together."

    ambrosia "Yes. Yes you did."

    "Antiope looks shocked. She is not used to seeing me angry, and especially not at her. She tries to calm down the game."

    antiope "I didn't realize I had already drunk so much. We celebrated a first time after the morning games and..."

    ambrosia "Antiope! That's not the problem."

    "I almost yelled the last sentence. I am furious."

    ambrosia "That's not the first time that happened! You eat and you drink as if trying to kill yourself by choking! I don't care about your traditions, this is just sick."

    "She stares at me with empty eyes. Stammers a few syllables. Stands up. Go to the pond. Splashes her face with cold water. Returns to the bench. Sits up again, looking a little better this time. And answers."

    antiope "Have you heard of this Spartan story with the fox?"

    "The unexpected change of topic troubles me for a second, but she does not sound like she is trying to simply avoid the conversation, so I respond as calmly as I'm currently able to."

    ambrosia "The one where a little boy would rather have his insides ripped apart by the fox he stole than to be caught red-handed? Yes, I know of it."

    antiope "Yes, that one. What do you think of it?"

    ambrosia "A gory tale tailored for a quick spread and a long-lasting legacy. Bloody, violent, shocking, everything a good urban legend needs to endure. It can also be interpreted as much as a tribute to the courage of Spartans as a denunciation of the cruelty of their education methods, and thus is told by both admirers and the enemies of this city."

    antiope "Ever thought it could be based on a true story? That's such a boy may have existed?"

    ambrosia "Actually... No. I don't think it's something that really happened. But it probably takes root in some real Spartan customs."

    antiope "Yes. It's a believable tale, because teaching how to steal to your future soldiers is actually a pretty rounded idea."

    "Antiope is speaking quickly, too quickly, each word mashing with the next, her accent more pronounced than ever."

    antiope "A marching army is made of thousands of people, all needing to eat. And most often than not, for the main body of the troops, after a few days, all their rations are gone and they have to live off the land. A ridiculous euphemism for stripping bare any field, any tree, any granary, any house."

    antiope  "For a nobody in such cohorts, the art of soulless plundering is perhaps more important than knowing how to handle your weapon. The main fight may never come for many stupid or noble reasons, but your stomach still aches every day."

    antiope "And, sometimes, an expedition can turn {i}really{/i} bad. The war may trail for so long that literally every edible thing around has already be ransacked. Despite the absence of real fighting, each new day brings in its share of cadavers as more and more people die from the many diseases corrupted water and rotten food bring. Apollo's wrath is far more lethal than any of Ares' bloodsheds."

    antiope "And to make things even worse, the suffering army can then be crushed utterly regardless of its many sacrifices, pushed into a retreat so chaotic that many small isolated groups are left behind, and must now make their way back on their own, in hunger and fear. For a perfect agony, you can even add some rain or snow."

    antiope "A lot more will die. But quite a number will also somehow survive. Among them, some will have sacrificed all morality, decency, humanity to escape the sirens of Inferno. They succeeded in returning to their home, but they will never be the same, their behavior forever changed."

    "Antiope suddenly stops talking and shuts her mouth tight. Her face shows a myriad of terrible expressions at once, hate, anger, fear, shame. She clings on her seat with both hands and applies such strength to it that she would probably crush it was it not made of stone. Her eyes are bottomless pits of darkness."

    "I hold my breath. I never saw her like that, and I'm actually terrified. She is right now a cornered beast, and I fear for what could happen with the wrong word, the wrong movement."

    menu:
        "Keep silent":
            jump glimpse
        "Comfort her":
            jump posttraumatic

label sick:
    "Antiope eats and drinks, drinks and eats, eats and drinks. I keep watching her, hypnotized, while she lives in her own world of never-ending binge."

    "However her body shows its limits. With each new plate, with each new cup, her movements slow down, her actions are more hesitant, more imprecise. Until finally, her febrile hands let go of a ripped fruit, which splashes onto the ground."

    "She looks at the stain with absent eyes for several couples of seconds, and suddenly she rises from her seat and tries to stagger away."

    show antiope at right with moveoutright

    "I rush to her side, but she shoves me aside, grumbling something unintelligible. She keeps stumbling by herself, only staying afoot thanks to helping walls."

    hide antiope with moveoutright

    "I follow her from two steps behind as she progresses slowly and with difficulty for long minutes."

    "At one point, she stops, perhaps because she simply cannot move anymore."

    window hide
    show bg inside as trans_inside:
        linear 2.0 xpos -config.screen_width
    show bg garden as trans_garden:
        xpos config.screen_width
        linear 2.0 xpos 0
    pause 2.0
    scene bg garden
    window show

    "We have reached a garden. A beautiful place, full of people earlier, now deserted due to the cold and the darkness. At this hour, every plant is displaying a different shade of gray instead of their usual brighter colors. But I have little time for contemplation as Antiope is busy throwing up on a flowerbed."

    "Once done, she walks a little straighter in direction of a nearby fountain, washes her face, then falls back into the green to empty herself a second time."

    show antiope:
        xalign 1.0 yalign 1.0
        linear 1.0 xalign 0.75
    pause 1.0
    show antiope:
        xalign 0.75

    "Finally {i}slightly{/i} better, she sits on a stone bench and stops moving altogether, her dead eyes looking at some point far far away."

    "I approach calmly, and ask softly if she is alright. She nods, still staring into nothingness. I ask again and she gives me the same soulless response, then adds."

    antiope "You can go. Everything's alright."

    "This only manages to increase my worries. Antiope is especially not well. She looks like she could break at any moment, and is only pretending to be fine because of her impossible stubbornness. I fear I will find her into small pieces tomorrow if I leave her like this, shivering in the cold when she can barely stand on her two legs."

    menu:
        "Insist to help":
            jump shake
        "Leave":
            jump flee

label posttraumatic:
    "I approach, and speak softly."

    ambrosia "It's okay."

    "And then, in a natural, gentle, void of any afterthoughts, movement, I put my hand on her shoulder. A friendly gesture to establish a physical bond upon a moral one."

    jump violence

label glimpse:
    "Time passes.{w} Slowly.{w}\n\nAntiope does not budge a muscle.{w} I don't dare moving either."

    "We stay like this for minutes? Hours? I can't say. I just know that after an eternity in the cold, Antiope finally stands up and goes away. Without a word, without even looking at me, she just walks away into the night."

    menu:
        "Chase after her":
            jump chase
        "Let her go":
            jump rooted

label rooted:
    "I'm unable to move. Silently, she continues to get away from this place, from me. She does not speak, she does not even bid me goodnight. She just blends into the darkness, her footsteps soon go silent, and I find myself alone outside in the cold."

    "I remain like this for several minutes, thinking about what happened, about the consequences of these events. But I'm too tired to properly put the pieces together and solve the riddle. So, I myself just go back to the building, my mind full of contradictory thoughts and feelings."

    jump oblivion

label chase:
    "I cannot let it end like that. I run after her. She does not quicken her pace, but she does not slow down either. In a few strides, I'm at her level. And then, in an idiotic reflex, I try to hold her back by grabbing her arm. An instinctive gesture, void of malice."

    jump violence

label violence:
    "A mistake."

    stop music
    play sound "sound/144015__satanicupsman__bone-flesh-crush-punch.ogg"

    show antiope angry:
        linear 0.5 zoom 4.0 yalign 0.075
    pause 0.5
    show antiope angry with hpunch:
        zoom 4.0 yalign 0.075

    "Antiope's reaction is as immediate as terrible. Her fingers instantly lock on my wrist with a bestial strength. She pushes back my arm like she was deflecting some sword blow, getting to her feet in the same movement. My heart starts beating furiously, from raw fear. I see death in her eyes, an unsheathed killing instinct."

    stop sound

    ambrosia "Antiope, please stop!"

    "My voice is a desperate plea. Panic gets the better of me, overriding all my conscious thoughts. I don't know what to do, but I'm trying to do it regardless, my whole body shaking."

    "I see Antiope's fist ready to strike."

    show antiope angry at center with fade:
        zoom 1.0 xalign 0.5 yalign 1.0

    "But before she throws it, the gods be thanked, some reason returns to her mind and she abruptly releases me. My now free body reflexively moves back a few steps, my hurt wrist pressed against my chest, the skin having turned red where she pressed it."

    play sound "sound/134991__zombiechaser3__running-sounds.ogg"

    show bg black as eclipse:
        alpha 0.0
        linear 1.0 alpha 1.0

    "And then I run. I run into the night, I run back to the building, back to the light and the people. I hide myself among all these anonymous persons, in constant movement, always out of sight. I don't know if she followed me, and I don't want to know if she did. I just want to be as far as possible from her, yet I refuse to go out while she may be somewhere out there."

    stop sound

    if dreamer:
        jump bramble
    else:
        jump apology

label drunkard:
    stop music fadeout 0.5
    show antiope with fade

    show bg inside with pixellate

    show antiope bliss with pixellate

    play music "music/Celestial_Aeon_Project_-_Harp_Music.ogg" fadein 2.0

    "Antiope's head lies on my shoulder, her arm holds my waist, our legs keep bumping into each other. I can literally feel the heat radiating from her hot and clinging body, her soft breath upon my neck. I feel {i}wonderful{/i}."

    "I support her and she supports me as we walk together through the rooms of the palace. The call of nature forced us to leave our table, and this non-savory problem solved, we now are going wherever our feet have decided to guide us to party more."

    window hide
    show bg inside as trans_inside:
        linear 2.0 xpos -config.screen_width
    show bg garden as trans_garden:
        xpos config.screen_width
        linear 2.0 xpos 0
    pause 2.0
    scene bg garden
    window show

    "Air is colder. Which is actually cool. Antiope and I have found a door opening to the outside. Garden. Lot of beautiful plants."

    show antiope bliss:
        zoom 2.0
        xalign 0.5 yalign 0.0

    "There is a bench. She releases my hip and lets herself fall down on it, stretched out upon the full width of the stone seat, her back against the rock and her head bobbing in the air."

    "I sit properly  – like the educated lady I am!  – then lean to look at her face from close. She displays an idiotic smile, her eyes glinting. The low angle makes her even more sexy, especially since she loosened her tunic to breath more freely. I could kiss her right now if I dare."

    menu:
        "Dare":
            jump kiss
        "Don't be stupid":
            jump fake

label fake:
    "I flounder a little. Antiope is as relaxed as she will ever be, so it's now or never."

    ambrosia "Antiope, there is something I must tell you."

    "She looks at me, a little surprised, but still blissfully happy."

    ambrosia "It's not easy to say..."

    antiope "Then sing it!"

    "I laugh. I haven't brought my lyre with me, but I stand up and start an impromptu rehearsal {i}a cappella{/i}."

    stop music fadeout 1.0

    ambrosia "With his venom{w}\nirresistible{w}\nand bittersweet"

    ambrosia "that loosener{w}\nof limbs, Love{w}\nreptile-like{w}\nstrikes me down"

    play music "music/Celestial_Aeon_Project_-_Harp_Music.ogg" fadein 1.0

    "Damn, I must be more tired than I thought. This sounds similar to the original poem, but some words are not what they should be. My teacher would have turned mad had she heard me. It's a personal interpretation of a classic, but not the classic itself."

    "Antiope doesn't really care about such technicalities however. She straightens her back to listen, and comments, with a mischievous smile."

    antiope "Ho, you have the hots for someone? And who is the lucky bastard?"

    "So long for the romantic atmosphere. I kneel a little so to look at her straight in the eyes."

    ambrosia "You."

    "Her bemused look is quite hilarious, but I refrain from laughing.{w}\n\nHowever, she does. "

    "It's a joyless, inebriate laugh, an unfunny sound which snatches my heart out of my chest and tears it apart without a single remorse."

    antiope "Me? {i}Me?!{/i} Sorry, not interested. Better luck next time."

    "And without a single drop of care, she just falls back to her drunken haze, dozing off her drunkenness on the stone, not totally asleep, but not totally conscious either."

    stop music

    "As for myself, I'm suddenly feeling very sober. Actually, I feel like I'm seeing the world with a never-experienced before clarity."

    "For example, I {i}should{/i} bring Antiope back inside before she catches a cold. But I'm already departing without her, letting her enjoy her friendly carved rock and the freshness of the night by herself."

    "A petty vengeance perhaps.{w} But it's either being angry, nasty, mean, or bursting into tears right on the spot."

    jump oblivion

label shake:
    "I get closer and put my hands upon her cheeks, forcing her to look at me. A gentle, soft move to bring her back to reality and give her a little of my own warmth."

    jump violence

label flee:
    "I continue to hesitate for a little more, but when she asked me again to leave, irritation making her voice rougher, I promptly obey."

    "On my way back, I turn around a few times to watch Antiope still perfectly immobile on the stone, catatonic, a faint flesh statue under the moon light."

    jump oblivion

label misconception:
    "I didn't really plan that much ahead, and I decide to just walk until I find an appropriate place. After several minutes of Antiope's complaining, our random meandering finally ends in the perfect location."

    window hide
    show bg inside as trans_inside:
        linear 2.0 xpos -config.screen_width
    show bg garden as trans_garden:
        xpos config.screen_width
        linear 2.0 xpos 0
    pause 2.0
    scene bg garden
    window show

    "The moon in the clear sky shines upon a beautiful garden, the pale light drawing many strange shadows for the gray flowers on each side of the lane, sometimes sparkling upon an early or late drop of dew. The path leads to a small fountain, with stone seats around for the tired and those needing to have a long, not rushed, talk. The air is really cold, but thanks to that the place is void of people."

    show antiope:
        xalign -0.25 yalign 1.0
        linear 1.0 xalign 0.75
    pause 1.0
    show antiope:
        xalign 0.75

    "Antiope passes beyond me and sits casually on a bench, her crossed legs resting on the edge of the pond."

    antiope "And now, are you ready to talk?"

    "I take my time to settle by her side, in a more formal and less provoking position."

    ambrosia "I think so."

    "I take a deep breath, and let the words flow in."

    ambrosia "The point I'm trying to get is that our relationship evolved. We started as strangers under the same roof, and we slowly but surely became the best of friends."

    "Antiope crosses her arm and appears to think."

    antiope "Quite a long introduction for an obvious statement. But I guess you're preparing for what's next. How our relationship should evolve from now on. I have been thinking about it myself."

    "I simply cannot hide that my heart started beating faster than a hummingbird flaps its wings. There is a seriousness I'm not used to in Antiope's voice as she continues."

    antiope "We've been going like this for... More than a year I guess? We've had our hardships, neither money or glory deigned rain upon us, but overall, it was a pretty nice adventure. We've had a good deal of fun, partook in some amazing events, including this one. I also learned a lot, and I think you did too."

    antiope "However, it's obvious, even for me, than we reached a turning point. I've checked our planning for the next months, and I couldn't miss the pattern. Except for a few surviving long trips, we're not going to move much. The reason for this is that you gained a bit of fame in the region, and several people have already commissioned you for their own events."

    antiope "This is incredible, but that's a clear sign you're about to set in. Soon, your patrons' support will be enough for you to stop having to travel for days to reach a miserable shrine in the middle of nowhere just in time for the big yearly festival, in hope it will boost your career and fill your purse. You're about to jump the big gap between poor errant artist and comfortable resident artist."

    antiope "Which of course means you will soon have no more use for a full-time scout."

    "I'm completely paralyzed, my tongue frozen inside my mouth. I didn't expect the conversation to take such a turn, and can only listen as Antiope continues her monologue."

    antiope "So I've searched for new opportunities. And I found some. Especially, there is a caravan going to Thrace which caught my interest, whose departure from Athens is planned in two months. I was getting a little bored with our usual moves, and a trip abroad sounds like a fun adventure."

    antiope "So, don't worry about me. I've got myself covered."

    "I... I think I didn't even breath for the whole duration of Antiope's speech. As for her, she picks herself up, ready to go."

    ambrosia "Did I cover all your worries, or is there something else you wanted to tell me?"

    "I... Antiope just explained to me, lengthily, how she prepared everything to quit me, and even if she made some efforts to make it looks like she does this for my own good, significant efforts actually for someone normally quite blunt, she couldn't help but remark she was starting to get bored."

    "My mind is overloading. I prepared myself for anything but that."

    antiope "Is there anything else you wanted to ask me? It's quite cold there."

    "The tone of her voice suggests that we should go back inside, where heat, food and drinks await us."

    menu:
        "Blankly give up":
            $ despair = True
            jump coward
        "Not so fast":
            jump fight

